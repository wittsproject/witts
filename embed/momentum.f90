! This module is for solving the momentum N-S equations
!
  MODULE momentum

  USE parameters
  USE class_shared
  USE tools
  USE convect
  USE viscous
  USE wall_model
  USE source
!  USE turbine
  USE coriolis
  USE gravity
  USE poisson

  CONTAINS 
!=============================================================================!
!	            UPDATE MOMENTUM EQUATION BY A-B SCHEME                    !
!=============================================================================!
!   UPDATE MOMENTUM BY USING PROJECTION METHOD:
!   A 4th-ORDER Runge-Kutta METHOD IS USED
    SUBROUTINE MOMENTUM_RK()

    IMPLICIT NONE 
    INTEGER :: M,MM,I,J,K
    REAL(KIND=DP),DIMENSION(:),ALLOCATABLE:: C,B
    REAL(KIND=DP),DIMENSION(:),ALLOCATABLE:: UN,VN,WN,UI,VI,WI
    REAL(KIND=DP) :: DX,DY,DZ
    
    ALLOCATE(C(ORDER_TIM),B(ORDER_TIM))
!---PARAMETERS FOR THE R-K SCHEME
    IF(ORDER_TIM.EQ.1)THEN
      B(1)=1.0
      C(1)=0.0
    ELSE IF(ORDER_TIM.EQ.2)THEN
      B(1)=0.0
      B(2)=1.0
      C(1)=0.0
      C(2)=0.5
    ELSE IF(ORDER_TIM.EQ.4)THEN
      B(1)=1.0/6.0
      B(2)=1.0/3.0
      B(3)=1.0/3.0
      B(4)=1.0/6.0
      C(1)=0.0
      C(2)=0.5
      C(3)=0.5
      C(4)=1.0
    END IF

    ALLOCATE(UN(TOTAL_CELL),VN(TOTAL_CELL),WN(TOTAL_CELL))
    ALLOCATE(UI(TOTAL_CELL),VI(TOTAL_CELL),WI(TOTAL_CELL))
    
    FX=0.0
    FY=0.0
    FZ=0.0
    DO M=1,TOTAL_CELL
      UN(M)=CELL_FV(M)%CELL_VEL(1)
      VN(M)=CELL_FV(M)%CELL_VEL(2)
      WN(M)=CELL_FV(M)%CELL_VEL(3)
      UI(M)=CELL_FV(M)%CELL_VEL(1)
      VI(M)=CELL_FV(M)%CELL_VEL(2)
      WI(M)=CELL_FV(M)%CELL_VEL(3)
    END DO   

    DO MM=1,ORDER_TIM  
      DO M=1,TOTAL_CELL
        CELL_FV(M)%CELL_FX=0.0
        CELL_FV(M)%CELL_FY=0.0
        CELL_FV(M)%CELL_FZ=0.0
      END DO  
!---GET WIND TURBINE BODY FORCE TERM    
!      IF(ITURBINE.EQ.1)THEN
!        CALL TURBINE_CELL_WRAP(DT*B(MM))
!      END IF
!---GET VISCOUS FORCES  
      CALL VISCOUS_CELL_WRAP()
!---GET CONVECTIVE FORCES
      CALL CONVECT_CELL_WRAP()
!---GET GRAVITY FORCE USING BOUSSINESQ APPROXIMATION
      IF(IBOUS.EQ.1)THEN
        CALL BOUSSINESQ()
      END IF 
!---GET CORIOLIS FORCES
      IF(ICORI.EQ.1)THEN
        CALL CORIO()
      END IF
!---SET SOURCE TERM
      CALL SOURCE_MOM()
!---CALCULATE THE INTERMEDIATE VELOCITY
      DO M=1,TOTAL_CELL
        CELL_FV(M)%CELL_VEL(1)=UN(M)+DT*CELL_FV(M)%CELL_FX
        CELL_FV(M)%CELL_VEL(2)=VN(M)+DT*CELL_FV(M)%CELL_FY
        CELL_FV(M)%CELL_VEL(3)=WN(M)+DT*CELL_FV(M)%CELL_FZ
      END DO  
!---SOLVE THE POISSON EQUATION TO GET THE PRESSURE GRADIENT TERM    
!      CALL PRESSURE_WRAP(DX,DY,DZ,DT) 
!---CORRECT THE VELOCITY (AND THE FLUXES)
      IF(MM.LT.ORDER_TIM)THEN
        DO M=1,TOTAL_CELL
          CELL_FV(M)%CELL_VEL(1)=UN(M)+C(MM+1)*DT*CELL_FV(M)%CELL_FX
          CELL_FV(M)%CELL_VEL(2)=VN(M)+C(MM+1)*DT*CELL_FV(M)%CELL_FY
          CELL_FV(M)%CELL_VEL(3)=WN(M)+C(MM+1)*DT*CELL_FV(M)%CELL_FZ
        END DO
!---UPDATE VELOCITY BOUNDARY CONDITIONS
        CALL GHOST_BOUNDARY(CELL_FV(:)%CELL_VEL(1),BC(:,1),BV(:,1))
        CALL GHOST_BOUNDARY(CELL_FV(:)%CELL_VEL(2),BC(:,2),BV(:,2))
        CALL GHOST_BOUNDARY(CELL_FV(:)%CELL_VEL(3),BC(:,3),BV(:,3))
      END IF

      DO M=1,TOTAL_CELL      
        UI(M)=UI(M)+B(MM)*DT*CELL_FV(M)%CELL_FX
        VI(M)=VI(M)+B(MM)*DT*CELL_FV(M)%CELL_FY
        WI(M)=WI(M)+B(MM)*DT*CELL_FV(M)%CELL_FZ
      END DO  
    END DO

    DO M=1,TOTAL_CELL
      CELL_FV(M)%CELL_VEL(1)=UI(M)
      CELL_FV(M)%CELL_VEL(2)=VI(M)
      CELL_FV(M)%CELL_VEL(3)=WI(M)
    END DO  
!---EXPAND VELOCITY FIELD
    CALL GHOST_BOUNDARY(CELL_FV(:)%CELL_VEL(1),BC(:,1),BV(:,1))
    CALL GHOST_BOUNDARY(CELL_FV(:)%CELL_VEL(2),BC(:,1),BV(:,2))
    CALL GHOST_BOUNDARY(CELL_FV(:)%CELL_VEL(3),BC(:,1),BV(:,3))
       
    DEALLOCATE(B,C,UN,VN,WN,UI,VI,WI)

    END SUBROUTINE MOMENTUM_RK
      
  END MODULE
